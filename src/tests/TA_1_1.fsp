/******************************************************************************

A travel agency sells vacation packages on-line by orchestrating existing
third-party web-services for different amenities (e.g. car rental, flight
purchase, hotel booking, etc). We want to synthesize an orchestrator based on
the requirements for the provision of vacation packages and the protocol
specification of the web-services.

The protocols for the services may vary, one variant is the number of steps
required for querying availability; in some cases the process requires a
multiple steps (e.g. querying for flight destination and dates, and if
successful following with a selection for class). Another variant is that some
services may require a reservation step which guarantees a purchase order for
a short period, while others do not, and hence the purchase may fail (e.g. on
low availability reservation may be disabled in order to maximize concurrency
between clients. As a result a race condition between two purchase orders may
arise, and therefore one order will fail).

The example scales in the following dimensions:

1- Amenities: The number of amenities included in the package.
2- Steps: The maximum number of steps requierd to query a service.

******************************************************************************/

const N = 1
const K = 1

const Amenities = N
const Steps = K

range R_Amenity   = 0..(Amenities-1)
range R_Step  = 0..(Steps-1)
range R_Bool = 0..1

/*****************************************************************************/
set Global_Alphabet = {select[r:R_Amenity],query[r:R_Amenity],available[r:R_Amenity],unavailable[r:R_Amenity], steps[r:R_Amenity][s:R_Step],
	query_succ[r:R_Amenity],query_fail[r:R_Amenity],commited[r:R_Amenity],uncommited[r:R_Amenity],
	cancel[r:R_Amenity],purchase[r:R_Amenity],purchase_succ[r:R_Amenity],purchase_fail[r:R_Amenity], agency_request, agency_succ, agency_fail}
set Controllable_Alphabet = {cancel[r:R_Amenity],purchase[r:R_Amenity], agency_succ, agency_fail}

Agency = (agency_request -> Processing),
  Processing = (
    {agency_fail,agency_succ} -> Agency |
    query[r:R_Amenity] -> Processing ).


Service[s_id:R_Amenity] = (
	{agency_succ,agency_fail} -> Service 
	|  query[s_id] -> Queried),
Queried = (unavailable[s_id] -> query_fail[s_id] -> Service 
	| available[s_id] -> steps[s_id][s:R_Step] -> query_succ[s_id] -> Selection[s]),
Selection[s:R_Step] =
	(when(s > 0)select[s_id] -> Selection[s-1]
	|when(s == 0)select[s_id] -> Booking),
Booking  = (
	when(s_id==(Amenities-1))     commited[s_id]   -> Reserve
	|when(s_id!=(Amenities-1)) query[s_id+1] -> Reserve
	|when(s_id==(Amenities-1)) uncommited[s_id] -> Direct
	|when(s_id !=(Amenities-1)) query[s_id+1] -> Direct),
Reserve  = (
	{agency_succ,agency_fail} -> Service
	|cancel[s_id]   -> Service
	|purchase[s_id] -> purchase_succ[s_id] -> Service ),
Direct  = (
	{agency_succ,agency_fail} -> Service
	|cancel[s_id]   -> Service
	|purchase[s_id] -> purchase_succ[s_id] -> Service
	|purchase[s_id] -> purchase_fail[s_id] -> Service ).


ServiceMonitor[s_id:R_Amenity] = (
	query[s_id]  -> InQuery        
	|agency_fail -> ServiceMonitor   ),
InQuery = (
	query_succ[s_id] -> QuerySucces
	|query_fail[s_id] -> QueryFail    
	|agency_fail -> ServiceMonitor   ),
QuerySucces = (
	purchase_succ[s_id] -> Success 
	|purchase_fail[s_id] -> QueryFail
	|cancel[s_id]        -> QueryFail
	|agency_fail -> ServiceMonitor   ),
QueryFail = (agency_fail -> ServiceMonitor   ),
Success = (agency_succ -> ServiceMonitor   |    agency_fail -> ServiceMonitor   ).
    
AgencyMonitor = Disallow[0],
Disallow[n:R_Bool] = (
	agency_succ -> Disallow[0]
	|query_fail[r:R_Amenity] -> Allow
	|when(n==0)uncommited[r:R_Amenity] -> Disallow[1] 
	|when(n==1)uncommited[r:R_Amenity] -> Allow
	|when(n==1) purchase_fail[r:R_Amenity] -> Allow ),
Allow = (
	{agency_fail,agency_succ} -> Disallow[0]
	|{query_fail[r:R_Amenity],uncommited[r:R_Amenity],purchase_fail[r:R_Amenity]} -> Allow ).


/*****************************************************************************/

||Plant = (Agency || AgencyMonitor || Service[a:R_Amenity] || ServiceMonitor[b:R_Amenity]).

Agency >> "/tmp/t_a_1_1_agency.rep".
Service_0 >> "/tmp/t_a_1_1_service_0.rep".
ServiceMonitor_0 >> "/tmp/t_a_1_1_service_monitor_0.rep".
AgencyMonitor >> "/tmp/t_a_1_1_agency_monitor.rep".
Plant >> "/tmp/t_a_1_1_plant.rep".
//marking = {agency.succ, agency.fail}
