/******************************************************************************

An airport control tower receives requests from planes trying to land.
The tower needs to signal them when it is safe to approach the ramps and at
which height they must perform holding maneuvers. If two airplanes enter
the same height space or ramp there is a chance a crash may occur.

We want a controller that automatically responds to airplanes requests and
ensures safe usage of the airport space.

The case study can be scaled in the following dimensions:
1- Planes: maximum number of simultaneous land requests.
2- Height: number of spaces above the ground where a plane can hold safely.

******************************************************************************/

const N = 3
const K = 2

const Planes  = N
const Heights = K

range R_Plane  = 0..(Planes-1)
range R_Height = 0..(Heights-1)

/*****************************************************************************/
set Global_Alphabet = {requestLand[p:R_Plane], extendFlight[p:R_Plane], land[p:R_Plane] , control_all, descend[p:R_Plane][h:R_Height], approach[p:R_Plane],air_crash,ramp_crash, on_route[p:R_Plane]}
set Controllable_Alphabet = {descend[p:R_Plane][h:R_Height], approach[p:R_Plane]}

Airplane[id:R_Plane] = (on_route[id] ->Airplane[id] |
    requestLand[id]-> descend[id][h:R_Height] -> Holding[h] |
    extendFlight[id]-> End),
  Holding[h:R_Height] =
    (when (h>0)  descend[id][h-1] -> Holding[h-1] |
      when(h == 0)approach[id] -> land[id] -> End),
  End = (control_all -> Airplane[id]).
HeightMonitor[id:R_Height] = Empty[id],
  Empty[h:R_Height] = (
    descend[p:R_Plane][h] -> Occupied[h] |
    when (h>0) descend[p:R_Plane][h-1] -> Empty[h] ),
  Occupied[h:R_Height] = (
      descend[p:R_Plane][h] -> CRASH |
      when(h > 0)descend[p:R_Plane][h - 1] -> Empty[h] |
      when(h == 0)land[p:R_Plane] -> Empty[h]),
CRASH= (air_crash->CRASH).


RampMonitor = Empty,
  Empty = (
    approach[p:R_Plane] -> Occupied ),
  Occupied = (
    approach[p:R_Plane] -> CRASH |
    land[p:R_Plane]     -> Empty ),
CRASH = (ramp_crash -> CRASH).

ResponseMonitor = (
  requestLand[p:R_Plane] -> descend[p][h:R_Height] -> ResponseMonitor |
  {extendFlight[p:R_Plane],descend[p:R_Plane][h:R_Height], approach[p:R_Plane]}  -> ResponseMonitor ).


//*****************************************************************************

||Plant = ( ResponseMonitor || RampMonitor || HeightMonitor[h:R_Height] || Airplane[p:R_Plane]).


|F|ENV = (Plant).

fluent EXTEND_FLIGHT[p:R_Plane] = <extendFlight[p], requestLand[p]> initially 1
fluent CONTROL_WILDCARD = <control_all, requestLand[p:R_Plane]>

|gr1| <{EXTEND_FLIGHT[p:R_Plane]}><{CONTROL_WILDCARD}>  NO_STRAT = ENV.

